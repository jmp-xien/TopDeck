#!/bin/csh

set suser = `id -u`
if ($suser != 0) then
    echo ERROR: TopDeck install must run as super user.
    exit(1)
endif

# FBSD Container Install Notes
echo "NOTE: About to begin install of TopDeck, FreeBSD container Admin Panel! Some setup information will be requested to complete the installation. Please provide the info as requested to properly intall TopDeck panel."
echo -n "Enter 'Y' to confirm: "
set tmpin0 = $<
echo ''
if ($tmpin0 == "Y" || tmpin0 == "y" ) then
    echo INFO: Installing TopDeck...
else
    exit(1)
endif

set startdir = `pwd`
set jl_bsdir = "/usr/jails"

echo "Intalling required packages..."
pkg install -y python38 py38-virtualenv mariadb105-server sudo

set rootmnt = `zfs list | egrep 'ROOT.*/$' | cut -d'/' -f1`
set rootzfs = `zfs list | egrep 'ROOT.*/$' | cut -d'/' -f2`

echo "Container setup process started..."
sleep 1

if ("$rootzfs" == "ROOT" ) then
    # ZFS
    echo "Creating ZFS mounts..."
    zfs create $rootmnt/usr/jails
    zfs create $rootmnt/usr/apps
    zfs create $rootmnt/usr/venv
    echo "Created required ZFS mounts..."
    sleep 2
else
    echo "Creating BSD Directories..."
    mkdir /usr/jails
    mkdir /usr/apps
    mkdir /usr/venv
    echo "Created BSD dirs..."
    sleep 2
endif

echo "Intalling needed directories..."
mkdir $jl_bsdir/baseroot
mkdir $jl_bsdir/config
mkdir $jl_bsdir/container
mkdir -p $jl_bsdir/template/usr

set logdir = '/var/log/topdeck'
mkdir $logdir
touch $logdir/topdeck.log

# Install FreeBSD System
echo "NOTICE: the root password provided during install of FreeBSD base, will be the same for any Container created by TopDeck"
echo -n "Press 'Enter' to continue: "
set tmpin = $<
echo "Intalling FreeBSD base template container..."
echo ''
bsdinstall jail $jl_bsdir/baseroot
sleep 2

cd $jl_bsdir/baseroot
chflags -R noschg var/empty
mv etc dev media mnt proc root tmp var $jl_bsdir/template/
mv usr/local usr/obj $jl_bsdir/template/usr/
mv .cshrc .profile COPYRIGHT $jl_bsdir/template/

cd $jl_bsdir/template
mkdir sysroot

set sysrdir="/sysroot"
foreach n ( bin boot lib libexec rescue sbin sys )
    ln -s $sysrdir/$n $n
end

foreach n ( bin games include lib lib32 libdata libexec ports sbin share src )
    ln -s $sysrdir/usr/$n usr/$n
end

# Start Dir to install environment
cd $startdir
echo ''
echo "..."
echo "Setting-up Python virtual environment..."
sleep 2
virtualenv /usr/venv/pyvenv

sleep 2
source /usr/venv/pyvenv/bin/activate.csh

sleep 2
pip install -r require.txt

sleep 2
ps aux | egrep 'mysql|mariadb' | cut -d' ' -f1 | grep mysql
if ($? == 0) then
    echo 'mysqld or mariadb is already running'
else
    /usr/local/etc/rc.d/mysql-server onestart
endif

sleep 2
# Install minimal setup database
mysql -e "create database topdeck"

sleep 2
mysql topdeck < topdeck.sql
tar -xvf TopDeck.tar -C /usr/apps/

# Set-up config files
set scr_dir = 'template/usr/local/scripts'
mkdir -p $jl_bsdir/$scr_dir

cd /usr/apps/TopDeck

echo ''
echo ">>> Provide a DNS/Name server IP Address <<<"
echo -n "DNS Server IP: "
set dnsipaddr = $<
echo '# Generated by TopDeck' > resolv.conf
echo "nameserver $dnsipaddr" >> resolv.conf
echo ''

cp shellinaboxd $jl_bsdir/$scr_dir/
cp periodic.conf $jl_bsdir/template/etc/
cp resolv.conf $jl_bsdir/template/etc/
cp uadd.csh  $jl_bsdir/$scr_dir/
cp uupd.csh  $jl_bsdir/$scr_dir/
cp base.conf $jl_bsdir/config/
cp jail.conf $jl_bsdir/config/
cp jail.conf /etc/

echo 'Username:topdeck' > topdeck.conf
echo ''
echo ">>> Create A Unique Password To Setup/Access TopDeck MySQL Database <<<"
echo -n "Password: "
set dbpwd = $<
echo "Password:$dbpwd" >> topdeck.conf
echo ''

# add account to database
echo "Configuring database..."
sleep 2
mysql -e "CREATE USER 'topdeck'@'localhost' IDENTIFIED BY '$dbpwd';"
mysql -e "GRANT ALL PRIVILEGES ON topdeck.* TO 'topdeck'@'localhost' IDENTIFIED BY '$dbpwd';"
mysql -e "FLUSH PRIVILEGES;"

sleep 2
echo "Creating run files..."
set hostip = `ifconfig | grep -A3 flags=88 | grep inet | cut -w -f3 | egrep '192.168.|10.' | head -n1`
mysql -e "UPDATE Server SET daship = '$hostip' WHERE id = 1;"
mysql -e "UPDATE Server SET dnsip = '$dnsipaddr' WHERE id = 1;"
sleep 1
echo '#\!/bin/sh' > temp.txt
echo ' ' >> temp.txt
echo TDIPADD=\"$hostip\" >> temp.txt
echo TDPORT=\"8001\" >> temp.txt
cat tdctl.csh >> temp.txt
mv temp.txt tdctl.csh
echo from topdeck import app > run.py
echo if __name__ == \"__main__\": >> run.py
echo "    "app.run\(\"$hostip\", 8001, debug = True\) >> run.py

echo ''
echo 'Installation of TopDeck Container Admin Panel is complete!'
echo 'To change default superuser account admin/Password01, go into TopDeck panel to edit.'
echo ''
echo 'Now running TopDeck in debug mode with command $ sudo /usr/apps/TopDeck/python run.py'
echo 'To run TD in prod mode run command $ sudo /usr/apps/TopDeck/tdctl.csh start'
echo 'Starting TopDeck....'
echo "Running TopDeck Container Admin Panel at http://${hostip}:8001"
echo ''
source /usr/venv/pyvenv/bin/activate.csh
/usr/venv/pyvenv/bin/python run.py
